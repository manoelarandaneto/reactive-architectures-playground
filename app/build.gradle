buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.17.0'
}

apply plugin: 'com.android.application'
apply plugin: 'com.github.ben-manes.versions'
apply from: '../code-quality/jacoco.gradle'

jacoco {
    toolVersion = "0.7.9"
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    flavorDimensions "envs"

    defaultConfig {
        applicationId "br.ufs.demos.rxmvp.playground"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        mock {
            applicationIdSuffix = ".mock"
            multiDexEnabled = true
        }
        live
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }

        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }
}

ext {
    dagger = '2.15'
    support = '27.1.1'
    retrofit2 = '2.4.0'
    butterknife = '8.8.1'
    lifecycle = '1.1.1'
    okhttp3 = '3.10.0'
    supporttest = '1.0.1'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "com.android.support:appcompat-v7:${support}"
    implementation "com.android.support:cardview-v7:${support}"
    implementation "com.android.support:recyclerview-v7:${support}"
    implementation "com.android.support:design:${support}"

    implementation "com.google.dagger:dagger:${dagger}"
    implementation "com.google.dagger:dagger-android-support:${dagger}"
    annotationProcessor "com.google.dagger:dagger-compiler:${dagger}"
    annotationProcessor "com.google.dagger:dagger-android-processor:${dagger}"

    implementation "com.squareup.retrofit2:retrofit:${retrofit2}"
    implementation "com.squareup.retrofit2:adapter-rxjava2:${retrofit2}"
    implementation "com.squareup.retrofit2:converter-gson:${retrofit2}"
    implementation 'io.reactivex.rxjava2:rxjava:2.1.12'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation "com.squareup.okhttp3:logging-interceptor:${okhttp3}"
    implementation 'com.github.akarnokd:ixjava:1.0.0'
    implementation 'com.binaryfork:spanny:1.0.4'

    implementation "com.jakewharton:butterknife:${butterknife}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${butterknife}"

    implementation "android.arch.lifecycle:runtime:${lifecycle}"
    implementation "android.arch.lifecycle:extensions:${lifecycle}"
    annotationProcessor "android.arch.lifecycle:compiler:${lifecycle}"

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.16.0'
    testImplementation "com.squareup.okhttp3:mockwebserver:${okhttp3}"
    testImplementation "org.robolectric:robolectric:3.6.1"
    testImplementation 'org.assertj:assertj-core:3.8.0'

    androidTestImplementation "com.android.support.test:runner:${supporttest}"
    androidTestImplementation "com.android.support.test:rules:${supporttest}"
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

    androidTestImplementation('com.schibsted.spain:barista:1.8.0') {
        exclude group: 'com.android.support'
    }
}


